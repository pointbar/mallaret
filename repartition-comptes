#!/usr/bin/env boot -f
; Ce script utilise http://boot-clj.com/

(set-env! :dependencies '[[clj-time "0.13.0"]
                          [org.clojure/clojure "1.8.0"]
                          [org.clojure/data.csv "0.1.4"]])

(require '[clj-time.core :as time]
         '[clj-time.local :as time-local]
         '[clojure.data.csv :as csv]
         '[clojure.java.io :as io]
         '[clojure.set :as set]
         '[clojure.string :as string])

(defn parse-blocks
  [lines]
  (-> (reduce (fn [state line]
                (if-let [title (some->> line (re-find #"^#(.*)") second string/trim)]
                  (-> state
                      (assoc :key title)
                      (update :titles #(conj % title)))
                  (update-in state [:data (:key state)] #(conj (or % []) line))))
              {:titles []}
              lines)
      (dissoc :key)))

(defn read-csv
  [rdr]
  (csv/read-csv rdr :separator \|))

(def trim-cells
  (partial map (partial map string/trim)))

(def remove-dashes
  (partial remove (comp (partial re-find #"^-+$") first)))

(defn csv-data->maps
  [csv-data]
  (let [header (first csv-data)]
    {:header header
     :data (map zipmap
                (->> header (map keyword) repeat)
                (rest csv-data))}))

(def parse-table
  (comp
    csv-data->maps
    remove-dashes
    trim-cells
    read-csv))

(defn current-year
  []
  (-> (time-local/local-now) time/year))

(defn date
  [day month]
  (time/local-date (current-year) month day))

(defn parse-date
  [date-string]
  (->> date-string
       (re-find #"0*([1-9][0-9]*)/0*([1-9][0-9]*)")
       rest
       (map read-string)
       (apply date)))

(def parse-personne
  keyword)

(def sens
  {"arrivée" :arrivee
   "départ" :depart})

(def libelles-sens
  (set/map-invert sens))

(defn parse-deplacement
  [x]
  {:date (-> x :Date parse-date)
   :voyageur (-> x :Voyageur parse-personne)
   :sens (-> x :Sens sens)})

(defn parse-prix
  [price-string]
  (when (seq price-string)
    (-> price-string
      (string/split #"[,.]")
      (->> (string/join "."))
      bigdec
      rationalize)))

(def repartitions
  {"Tout le monde" :tous
   "Les présent/e/s" :presentes
   "Les gîtants" :gitants})

(def libelles-repartition
  (set/map-invert repartitions))

(defn parse-depense
  [depense]
  (let [{:keys [Id Date Payeur Titre Fournisseur Prix Répartition]} depense]
    {:id (read-string Id)
     :date (parse-date Date)
     :payeur (parse-personne Payeur)
     :titre Titre
     :fournisseur Fournisseur
     :prix (parse-prix Prix)
     :repartition (repartitions Répartition)}))

(defn parse-soldes
  [soldes]
  (into {}
    (map (fn [[personne solde]]
          [(parse-personne personne) (parse-prix solde)])
         soldes)))

(defn parse-transaction
  [transaction]
  {:montant (-> transaction :Montant parse-prix)
   :transaction (-> transaction :Transaction read-string)
   :soldes (-> transaction
               (dissoc :Montant :Transaction)
               parse-soldes)})
(def input
  {:depenses parse-depense
   :deplacements parse-deplacement
   :transactions parse-transaction})

(def parse-line
  {:depenses parse-depense
   :deplacements parse-deplacement
   :transactions parse-transaction})

(defn parse-block
  [key block]
  (update block :data (partial map (key parse-line))))

(def titres-blocks
  {"Dépenses" :depenses
   "Arrivées et départs" :deplacements
   "Soldes" :transactions})

(defn read-comptes
  [f]
  (with-open [rdr (io/reader f)]
    (let [blocks (-> rdr line-seq parse-blocks)]
      (reduce (fn [blocks [titre clef]]
                (assoc blocks clef (->> (get-in blocks [:data titre])
                                        (remove empty?)
                                        (string/join "\n")
                                        parse-table
                                        (parse-block clef))))
              blocks
              titres-blocks))))

(defn participants
  [transactions]
  (-> transactions first :soldes keys))

(defn renumber-depenses
  [comptes]
  (update-in comptes [:depenses :data] #(map (fn [depense id] (assoc depense :id id)) % (->> (range) (drop 1)))))

(defmulti repartition (fn [_ depense] (:repartition depense)))

(defn repartition-parmi
  [personnes depense]
  (let [{:keys [payeur prix]} depense
        nb-personnes (count personnes)]
   (-> (zipmap personnes (repeat (-> prix (/ nb-personnes) -)))
       (assoc payeur (-> prix (* (dec nb-personnes)) (/ nb-personnes))))))

(defmethod repartition :tous
  [comptes depense]
  (repartition-parmi (-> comptes :transactions :data participants) depense))

(defn update-presentes
  [presents deplacement]
  ((-> deplacement :sens {:arrivee conj :depart disj}) presents (:voyageur deplacement)))

(defn presentes
  [deplacements date]
  (->> deplacements
       (filter #(not (time/after? (:date %) date)))
       (reduce update-presentes #{})))

(defmethod repartition :presentes
  [comptes depense]
  (let [{:keys [date]} depense]
    (repartition-parmi (-> comptes :deplacements :data (presentes date)) depense)))

(defmethod repartition :gitants
  [comptes depense]
  (repartition-parmi #{:fred :solenne :manu :xavier :agnes} depense))

(defn update-soldes
  [soldes depense-repartie]
  (reduce (fn [soldes [personne depense]] (update soldes personne #(+ % depense))) soldes depense-repartie))

(defn soldes-initiaux
  [personnes]
  (zipmap personnes (repeat 0)))

(defn progression-depenses
  [depenses-reparties soldes-initiaux]
  (reductions update-soldes soldes-initiaux depenses-reparties))

(defn garde-soldes-modifies
  [depenses soldes]
  (select-keys soldes (keys depenses)))

(defn ajoute-id-et-montant
  [depenses soldes]
  {:transaction (:id depenses)
   :montant (:prix depenses)
   :soldes soldes})

(defn calcule-transactions
  [comptes]
  (let [depenses (-> comptes :depenses :data)
        depenses-reparties (map (partial repartition comptes) depenses)]
    (->> comptes :transactions :data participants soldes-initiaux
         (progression-depenses depenses-reparties)
         rest
         (map garde-soldes-modifies depenses-reparties)
         (map ajoute-id-et-montant depenses))))

(defn update-transactions
  [comptes]
  (assoc-in comptes [:transactions :data] (calcule-transactions comptes)))

(defn format-date
  [local-date]
  (.toString local-date "dd/MM"))

(defn format-prix
  [prix]
  (when prix
    (format "%.2f" (double prix))))

(def format-personne
  name)

(defn pad
  [string length transfo]
  (->> (repeat " ") (concat (transfo string)) (take length) transfo (apply str)))

(defn rpad
  [string length]
  (pad string length identity))

(defn lpad
  [string length]
  (pad string length reverse))

(defn format-depense
  [depense]
  {"Id" (-> depense :id str)
   "Date" (-> depense :date format-date)
   "Payeur" (-> depense :payeur format-personne)
   "Titre" (-> depense :titre)
   "Fournisseur" (-> depense :fournisseur)
   "Prix" (->> depense :prix format-prix)
   "Répartition" (-> depense :repartition libelles-repartition)})

(def padding-depense
  {"Id" lpad
   "Date" lpad
   "Payeur" rpad
   "Titre" rpad
   "Fournisseur" rpad
   "Prix" lpad
   "Répartition" rpad})

(defn format-deplacement
  [deplacement]
  {"Date" (-> deplacement :date format-date)
   "Voyageur" (-> deplacement :voyageur format-personne)
   "Sens" (-> deplacement :sens libelles-sens)})

(def padding-deplacement
  {"Date" lpad
   "Voyageur" rpad
   "Sens" rpad})

(defn format-transaction
  [transaction]
  (into {"Transaction" (-> transaction :transaction str)
         "Montant" (-> transaction :montant format-prix)}
    (->> transaction :soldes (map (fn [[personne solde]] [(format-personne personne)
                                                          (format-prix solde)])))))

(def padding-transaction
  (constantly lpad))

(defn rows->columns
  [table]
  (apply merge-with conj
         (zipmap (-> table first keys) (repeat nil))
         table))

(defn max-length
  [column]
  (->> column
       (map count)
       (apply max)))

(defn calcule-longueurs
  [table]
  (into {} (map (fn [[k v]] [k (max-length v)])) (rows->columns table)))

(defn format-table
  [headers data]
  (-> (map #(->> (map % headers) (string/join " | ") string/trimr)
           data)
      (concat [""])))

(defn make-tirets
  [longueurs]
  (into {} (map (fn [[k v]] [k (->> (repeat "-") (take v) (apply str))])
                longueurs)))

(defn pad-row
  [padders longueurs row]
  (into {} (map (fn [[k v]] [k ((padders k) (row k) v)])
                longueurs)))

(defn pad-table
  [table pad-header pad-data]
  (cons (-> table first pad-header)
        (->> table rest (map pad-data))))

(defn format-data
  [data formatter padders]
  (let [headers (:header data)
        table (->> data 
                   :data 
                   (map formatter)
                   (concat [(zipmap headers headers)]))
        longueurs (calcule-longueurs table)
        table (pad-table table (partial pad-row (constantly rpad) longueurs) (partial pad-row padders longueurs))
        tirets (make-tirets longueurs)
        table (concat [(first table) tirets] (rest table))]
    (format-table headers table)))

(def output
  {:depenses {:format format-depense :pad padding-depense}
   :deplacements {:format format-deplacement :pad padding-deplacement}
   :transactions {:format format-transaction :pad padding-transaction}})

(defn format-paragraph
  [comptes key]
  (let [{:keys [format pad]} (output key)]
    (format-data (key comptes) format pad)))

(defn update-blocks
  [comptes]
  (reduce (fn [comptes [titre clef]]
            (assoc-in comptes [:data titre] (format-paragraph comptes clef)))
          comptes
          titres-blocks))

(defn ensure-ends-with-newline
  [s]
  (if (= \newline (get s (-> s count dec)))
    s
    (str s "\n")))

(defn format-comptes
  [comptes]
  (->> (:titles comptes)
       (mapcat (fn [titre] (cons (str "# " titre) (get-in comptes [:data titre]))))
       (string/join "\n")
       ensure-ends-with-newline))

(defn update-comptes
  [filename]
  (->> filename
      read-comptes
      renumber-depenses
      update-transactions
      update-blocks
      format-comptes
      (spit filename)))

(defn -main [& args]
  (update-comptes "comptes.md"))
